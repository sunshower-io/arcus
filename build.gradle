import org.gradle.api.publish.maven.MavenPublication

plugins {
    id 'pmd'
    id 'groovy'
    id 'java-library'
    id "com.github.spotbugs" version "4.7.0"
    id "io.freefair.lombok" version "5.3.0"
    id "com.diffplug.spotless" version "5.11.0"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}


allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenLocal()
        mavenCentral()
    }


    dependencyManagement {
        imports {
            mavenBom "io.sunshower.arcus:bom-imported:$version"
        }
    }


}

subprojects {
    apply plugin: 'pmd'
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'java-library'
    apply plugin: 'io.freefair.lombok'
    apply plugin: "com.github.spotbugs"
    apply plugin: "com.diffplug.spotless"

    spotbugsMain {
        reports {
            html {
                enabled = true
            }
        }
    }

    spotbugsTest {
        enabled = false
    }

    pmd {
        sourceSets = [sourceSets.main]
        ruleSets = [
                "${project.rootDir}/check/pmd/rules/errorprone.xml"
        ]
    }

    test {
        useJUnitPlatform()
    }

    spotless {
        java {
            googleJavaFormat('1.8').aosp()
        }
    }

    task generateDocumentation(type: Javadoc) {
        source = sourceSets.main.allJava
        classpath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath
    }



    publishing {

        repositories {
            mavenLocal()

            maven {
                credentials {
                    username "$mavenRepositoryUsername"
                    password "$mavenRepositoryPassword"
                }
                url "$mavenRepositoryUrl"
            }
        }



        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version
                from components.java
            }
        }
    }


    dependencies {

        testImplementation 'org.spockframework:spock-core'
        [
                'api',
                'engine'

        ].each { dep ->
            testImplementation "org.junit.jupiter:junit-jupiter-$dep"
        }
    }

}


defaultTasks(
        'clean',
        'build',
        'spotlessApply',
        'generateDocumentation'
)
